@NonCPS
def getPassive(json) {
	def matcher = new groovy.json.JsonSlurper().parseText(json).spec.to.name =~ /(green|blue)(.+)/
	String namespace = matcher[0][1]
	return namespace.equals("green") ? "blue" : "green" 
}

@NonCPS
def getRouteName(json) {
	return new groovy.json.JsonSlurper().parseText(json).items[0].metadata.name
}

@NonCPS
def getVersions(json) {
	def tags = new groovy.json.JsonSlurper().parseText(json).status.tags
	def versions = []
	for (int i = 0; i < tags.size(); i++) {
		versions << tags[i]['tag']
	}
	return versions
}

@NonCPS
def recentVersion(List versions) {
	versions.sort( false ) { a, b ->
		[a,b]*.tokenize('.')*.collect { it as int }.with { u, v ->
			[u,v].transpose().findResult{ x,y-> x<=>y ?: null } ?: u.size() <=> v.size()
		}
	}[-1]
}

@NonCPS
def getTestStatus(json) {
	return new groovy.json.JsonSlurper().parseText(json).metadata.labels."test-passed"
}

def testStatus(project) {
	sh "oc get project ${project} -o json > test.json"
	def test = readFile('test.json')
	return getTestStatus(test)
}

def getLatestVersion(project, microservice) {
	sh "oc get is ${microservice} -o json -n ${project} > image.json"
	def image = readFile('image.json')
	def versions = getVersions(image)
	if (versions.size() == 0) {
		error("There are no images for ${microservice}")
	}
	return recentVersion(versions)
}

node("maven") {

	properties([
	  parameters([
	     string(name: 'GITHUB'), string(name: 'PRODUCT'), string(name: 'REPO'), string(name: 'MICROSERVICE'), string(name: 'PROJECT'),
	  ])
	])
	
	def prevProject = params.PROJECT
	def project = "${params.PRODUCT}-prod"
	def microservice = params.MICROSERVICE
	def product = params.PRODUCT
	def dbProject = "${params.PRODUCT}-database"
	def version
	def env
	def pipelines

	stage("determine the environment to deploy to") {
		sh "oc get routes -l product=${product} -o json -n ${project} > routeList.json"
		def routeList = readFile('routeList.json')
		def routeName = getRouteName(routeList)
		sh "oc get route ${routeName} -o json -n ${project} > route.json"
		def route = readFile('route.json')
		env = getPassive(route)
		println "the target environment is $env"
	}
	
	stage ("determine the status of the ${params.PROJECT} environment") {
		testStatus = testStatus prevProject
		println "the ${prevProject} environment test status is $testStatus"
		if (testStatus.equals("false")) error("Cannot promote $env microservices to staging as they have not passed ${params.PROJECT} testing")
	}		
	
	stage("determine which image is to be deployed") {
		version = getLatestVersion prevProject, microservice
		println "latest version is $version"
	}

	stage("promote the image from ${prevProject} to ${project}") {
		openshiftTag namespace: prevProject, srcStream: microservice, srcTag: version, destinationNamespace: project, destinationStream: microservice, destinationTag: version
		sh "oc patch is/${microservice} -p '{\"metadata\":{\"labels\":{\"product\":\"${product}\"}}}' -n ${project}"
	}

	stage("checkout release version") {
		checkout scm: [$class: 'GitSCM', 
      userRemoteConfigs: [[url: "https://github.com/${params.GITHUB}/${params.REPO}"]], 
      branches: [[name: "refs/tags/${version}"]]], changelog: false, poll: false
	}

	stage("read the pipeline definition") {
		pipelines = readYaml file: "openshift/pipelines/pipelines.yml"
	}	
	
	stage("reset test flag for ${project}") {
		sh "oc label namespace ${project} test-passed=false --overwrite=true"	
	}	

	stage("promote the image from ${params.PROJECT} to ${project}") {
		openshiftTag namespace: params.PROJECT, srcStream: microservice, srcTag: version, destinationNamespace: project, destinationStream: microservice, destinationTag: version
		sh "oc patch is/${microservice} -p '{\"metadata\":{\"labels\":{\"product\":\"${params.PRODUCT}\"}}}' -n ${project}"
	}	
	
	stage("remove the deployment, configuration map and secrets") {
		sh "oc delete dc ${env}${microservice} -n ${project} || true"	
		sh "oc delete configmap ${env}${microservice} -n ${project} || true"
		sh "oc delete secret ${env}${microservice} -n ${project} || true"	
	}

	stage("create the configuration map") {
		def yml = readYaml file: "openshift/configuration/prod-${env}-config.yml"
		def configMap = yml.config_map
		def mapStr = ""
		configMap.each { config ->
			mapStr = "${mapStr} --from-literal=${config.name}=${config.value}"
		}
		sh "oc create configmap ${env}${microservice} ${mapStr} -n ${project}"
	}

	if (fileExists("openshift/configuration/prod-${env}-secret.yml")) {
		stage("create the secrets") {
			def yml = readYaml file: "openshift/configuration/prod-${env}-secret.yml"
			def secrets = yml.secrets
			def mapStr = ""
			secrets.each { secret ->
				mapStr = "${mapStr} --from-literal=${secret.name}=${secret.value}"
			}
			sh "oc create secret generic ${env}${microservice} ${mapStr} -n ${project}"
		}
	}

	if (pipelines.promote.db) {
		stage("ensure the database exists") {
			withMaven(mavenSettingsConfig: 'microservices-scrum') {
		      sh "mvn clean package -P create-prod-db -Dmaven.test.skip=true -Dproject=${project} -Ddb.project=${dbProject}"
		    } 
		}
	}

	stage("read the resource definition") {
		if (fileExists("openshift/configuration/prod-${env}-resources.yml")) {
			def yml = readYaml file: "openshift/configuration/prod-${env}-resources.yml"
			resources = yml.resources
		} else {
			def yml = readYaml file: "openshift/configuration/default-resources.yml"
			resources = yml.resources
		}
	}

	stage("create deployment config") {
		sh "oc process -n ${project} -f openshift/templates/${microservice}-config.yml -p NAMESPACE=${project} -p ENV=${env} -p DOCKER_NAMESPACE=${project} -p DOCKER_IMAGE_LABEL=${version} -p PRODUCT=${params.PRODUCT} -p MAX_MEMORY=${resources.max_memory} -p MIN_MEMORY=${resources.min_memory} -p MAX_CPU=${resources.max_cpu} -p MIN_CPU=${resources.min_cpu} -p REPLICAS=${resources.replicas} | oc apply -f -"
	}
	
	stage("execute deployment") {
		openshiftDeploy namespace: project, depCfg: "${env}${microservice}",  waitTime: "3000000"
		openshiftVerifyDeployment namespace: project, depCfg: "${env}${microservice}", replicaCount:"1", verifyReplicaCount: "true", waitTime: "300000" 
	}

}