@NonCPS
def getVersion(pom) {
	def matcher = new XmlSlurper().parseText(pom).version =~ /(\d+\.\d+\.)(\d+)(\-SNAPSHOT)/
	return "${matcher[0][1]}${matcher[0][2].toInteger()}-SNAPSHOT"
}

@NonCPS
boolean dcexists(json, microservice) {
	def items = new groovy.json.JsonSlurper().parseText(json).items
	for (int i = 0; i < items.size(); i++) {
		def name = items[i]['metadata']['name']
		if (name.equals(microservice)) {
			return true;
		}
	}
	return false
}

boolean deploymentConfigExists(project, microservice) {
	sh "oc get dc -o json -n ${project} > dc.json"
	def dc = readFile('dc.json')
	return dcexists(dc, microservice)
}

node("maven") {

	properties([
	  parameters([
	     string(name: 'GITHUB'), string(name: 'PRODUCT'), string(name: 'REPO'), string(name: 'MICROSERVICE'),
	  ]),
	  disableConcurrentBuilds()
	])

	def project = "${params.PRODUCT}-build"
	def microservice = params.MICROSERVICE	
	def version
	def pipelines

	currentBuild.description = "Build a container from the source, then execute unit and container integration tests before promoting the container as a release candidate for acceptance testing."

	stage("checkout") {
		git branch: "master", url: "https://github.com/${params.GITHUB}/${params.REPO}"
	}
	
	stage("read the pipeline definition") {
		pipelines = readYaml file: "openshift/pipelines/pipelines.yml"
	}

	stage("remove the previous deployment") {
		if (deploymentConfigExists(project, microservice)) {
			sh "oc delete dc ${microservice} -n ${project}"	
		}
	}

	if (pipelines.build.restart[0] != null) {
		stage("restart ${pipelines.build.restart[0]}") {
			def service = pipelines.build.restart[0]
			openshiftDeploy namespace: project, depCfg: service
			openshiftVerifyDeployment namespace: project, depCfg: service, replicaCount:"1", verifyReplicaCount: "true", waitTime: "300000"    	
		}		
	}

	if (pipelines.build.wiremock[0]) {
		stage("update wiremock") {
			def files = findFiles(glob: 'src/integration-test/resources/*.json')
			files.each { file -> 
				def json = readFile(file.path)
				def response = httpRequest url: "http://wiremock-docker.${project}.svc:8080/__admin/mappings/new", httpMode: "POST", validResponseCodes: "201", requestBody: json
			}
		}		
	}

	if (pipelines.build.tests[0]) {
		stage("unit tests") {
			withMaven(mavenSettingsConfig: 'microservices-scrum') {
		     sh "mvn clean test"
		  } 
		}		
	}
	
	if (pipelines.build.db[0]) {
		stage("prepare the database") {
			withMaven(mavenSettingsConfig: 'microservices-scrum') {
				sh "mvn clean package -P prepare-db -Dmaven.test.skip=true -Dproject=${project}"
			}     	
		}		
	}
	
	if (pipelines.build.mq[0]) {
		stage("reset a-mq to purge topics") {
			openshiftDeploy namespace: project, depCfg: "broker-amq"
			openshiftVerifyDeployment namespace: project, depCfg: "broker-amq", replicaCount:"1", verifyReplicaCount: "true", waitTime: "300000"    	
		}		
	}
	
	stage("reset the promoted image stream") {
		def pom = readFile('pom.xml')
		version = getVersion(pom)
		sh "oc tag -d ${microservice}:${version} -n ${params.PRODUCT}-cicd || true"
	}	
	
	stage("create build config") {
			sh "oc process -n ${project} -f openshift/templates/${microservice}-build-config.yml -p NAMESPACE=${project} -p GITHUB=${params.GITHUB} -p DOCKER_IMAGE_LABEL=latest -p PRODUCT=${params.PRODUCT} | oc apply -f -"
	}

	stage("execute build") {
		openshiftBuild namespace: project, buildConfig: microservice
		openshiftVerifyBuild namespace: project, buildConfig: microservice, waitTime: "300000" 
	}

	stage("create deployment config") {
		sh "oc process -n ${project} -f openshift/templates/${microservice}-config.yml -p NAMESPACE=${project} -p DOCKER_NAMESPACE=${project} -p DOCKER_IMAGE_LABEL=latest -p PRODUCT=${params.PRODUCT} | oc apply -f -"
		if (pipelines.build.wiremock[0]) {
			def envVars = ""
			pipelines.build.wiremock_environment_variables[0].each {
				envVars = "${envVars} ${it['name']}=${it['value']}"
			}
			sh "oc set env dc/${microservice} ${envVars} -n ${project}"	
		}
	}

	stage("execute deployment") {
		openshiftDeploy namespace: project, depCfg: microservice
		openshiftVerifyDeployment namespace: project, depCfg: microservice, replicaCount:"1", verifyReplicaCount: "true", waitTime: "300000" 
	}

	if (pipelines.build.tests[0]) {
		stage("execute the container tests") {
			withMaven(mavenSettingsConfig: 'microservices-scrum') {
				try {
					sh "mvn clean verify -P integration-test"
				} finally {
					if (pipelines.build.mq[0]) {
						sh "oc set env dc/${microservice} JBOSS_A_MQ_BROKER_URL=tcp://localhost:61616 -n ${project}"	
					}
				}
			}    		
		}		
	}
	
	if (pipelines.build.promote[0]) {
		stage("promote the image to cicd") {
			openshiftTag namespace: project, srcStream: microservice, srcTag: "latest", destinationNamespace: "${params.PRODUCT}-cicd", destinationStream: microservice, destinationTag: version
			sh "oc patch is/${microservice} -p '{\"metadata\":{\"labels\":{\"product\":\"${params.PRODUCT}\"}}}' -n ${params.PRODUCT}-cicd"
		}		
	}

}

